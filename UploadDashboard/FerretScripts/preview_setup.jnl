! preview_setup.jnl
!
! Setup for preview plots
! Open the dataset, define the output file name
! Make definitions used by all the scripts

! Open the input dataset
IF `($FERRET_VERSION) GE 7.51` THEN
    USE /FEATURE=NONE "($infile_dsg)"
ELSE
    USE "($infile_dsg)"
ENDIF

! Define the plot file name
DEFINE SYMBOL expocode_str = `expocode`

SET VAR/TITLE="WOCE flag for water CO_2" WOCE_CO2_water
SET VAR/TITLE="fCO_2 recommended" fCO2_recommended


! reset the green pen to a darker green...
ppl color,3,0,80,0    

! reset the blue pen to a lighter blue ...
ppl color,4,20,30,100   

! land map palette
DEFINE SYMBOL land_palette = gray_light

! Reset color 6 to light gray - line underlay on map, marking woce-4 points
ppl color,6,70,70,70 ! gray


! set up the color palette for WOCE flags
DEFINE SYMBOL woce_palette = woce2



can mode logo

ppl txlsze,0.08
ppl axlsze,0.08,0.08


LET nobs = `latitude[i=@ngd]`
LET nfnoc = `fCO2_recommended[i=@ngd]`


! Define time axis
! If we cannot define the time axis, exit
SET MODE ignore
DEFINE AXIS/T/T0="1-Jan-1970"/UNITS=seconds tax = time
IF ($fer_last_error"0|*>1") THEN exit/script

CANCEL MODE ignore

let tt = t[gt=tax]; 
let tt1 = `tt[l=@min]`
let ttn = `tt[l=@max]`
LET monthsecs = 30*24*60*60
!IF `ttn-tt1 GT monthsecs` THEN DEFINE SYMBOL taxis_setting = PPL txtype,MON,DMY

! Define numeric variable on the time axis with the WOCE flags
SET VAR/NAME=WOCE_CO2_water_in WOCE_CO2_water
LET WOCE_CO2_water_x = STRFLOAT(WOCE_CO2_water_in)
LET WOCE_CO2_water = RESHAPE(WOCE_CO2_water_x, tt)



LET var_sources = {\
"xCO2_water_equi_temp_dry_ppm", \
"xCO2_water_sst_dry_ppm", \
"pCO2_water_equi_temp", \
"pCO2_water_sst_100humidity_uatm", \
"fCO2_water_equi_uatm", \
"fCO2_water_sst_100humidity_uatm", \
"pCO2_water_equi_temp", \
"pCO2_water_sst_100humidity_uatm", \
"xCO2_water_equi_temp_dry_ppm", \
"xCO2_water_sst_dry_ppm", \
"xCO2_water_equi_temp_dry_ppm", \
"xCO2_water_sst_dry_ppm", \
"xCO2_water_equi_temp_dry_ppm", \
"xCO2_water_sst_dry_ppm"}

LET source_labels = {\
"xCO_2^s^w(teq) dry", \
"xCO_2^s^w(sst) dry", \
"pCO_2^s^w(teq)", \
"pCO_2^s^w(sst)", \
"fCO_2^s^w(teq)", \
"fCO_2^s^w(sst)", \
"pCO_2^s^w(teq) ncep", \
"pCO_2^s^w(sst) ncep", \
"xCO_2^s^w(teq) dry woa", \
"xCO_2^s^w(sst) dry woa", \
"xCO_2^s^w(teq) dry ncep", \
"xCO_2^s^w(sst) dry ncep", \
"xCO_2^s^w(teq) dry ncep_woa", \
"xCO_2^s^w(sst) dry ncep_woa"}

define view/x=0:1/y=0:1 full0
define view/x=0:1/y=0:1 full1
define view/x=0:1/y=0:1 full2
define view/x=0:1/y=0:1 full3

cancel mode nodata_label


! polygon plot style 
DEFINE SYMBOL ppstyle = /SYMBOL=17/siz=0.06
! line plot style 
DEFINE SYMBOL linestyle = /LINE/SYMBOL=21/siz=0.02
! Style for marking WOCE 4 locations
DEFINE SYMBOL woce4style = /SYMBOL=18/thick=1/siz=0.08/color=6

