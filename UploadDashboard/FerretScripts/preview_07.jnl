! preview_07.jnl
!
! Salinity
go preview_setup


! Plot Salinity variables with a common vertical axis


let/title="WOA sss" woa = reshape(woa_sss, tt)
let/title="Sal"/units="`sal,return=units`" sal_t = reshape (sal, tt)

! Plot together to get scale for vertical axis
PLOT woa, sal_t

CANCEL VIEW
SET VIEW full

LET amin = ($yaxis_min)
LET amax = ($yaxis_max)
GO preview_xy_range amin amax 1

LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)

DEFINE SYMBOL vlimits = /vlim=`yax_lo`:`yax_hi`

set view full

define symbol style2 = /color=red/thick=2/DASH=(0.04,0.04,0.04,0.04)
define symbol leglab2 WOA sss

plot($linestyle)($style2)($vlimits)/title=" "/set woa
  ppl ylab Salinity (`woa_sss,return=units`)
  ppl xlab " "
  ($taxis_setting)
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
ppl plot


define symbol style1 = /color=blue/thick=2
define symbol leglab1  Sal

plot/over/nolab($linestyle)($style1) sal_t

! Label at the top with the Expocode and count
DEFINE SYMBOL lab_top = Expocode `EXPOCODE`
LABEL/NOUSER  `($ppl$xlen)/2`,`($ppl$ylen)+0.6*($ppl$yorg)`, 0,0,0.13,  ($lab_top)

LABEL/NOUSER  `($ppl$xlen)/2`,`($ppl$ylen)+0.3*($ppl$yorg)`, 0,0,0.10, Salinities  `nobs` locations


! If no data, add a label
IF `woa[L=@ngd] + sal_t[L=@ngd] EQ 0` THEN \
   LABEL/nouser `($ppl$xlen)/2`,`($ppl$ylen)/2`, 0, 0, 0.15, No Valid Data

go keybox ($style1) "($leglab1)" ($style2) "($leglab2)" 

FRAME/FILE="($result_plot_image_filename)"

