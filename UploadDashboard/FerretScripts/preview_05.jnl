! preview_05.jnl
!
! Temperatures

go preview_setup

! Plot together to get scale for LHS axis
PLOT reshape(temperature_equi, tt), reshape(temp, tt)

CANCEL VIEW
SET VIEW full

LET amin = ($yaxis_min)
LET amax = ($yaxis_max)
GO preview_xy_range amin amax 1

LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)

DEFINE SYMBOL vlimits = /vlim=`yax_lo`:`yax_hi`

! Plot temperature variables each with its own vertical axis

let/title="Temp intake" tempr = reshape(temp, tt)
let/title="Temp diff" dtemp = reshape(delta_temp, tt)
let/title="Temp equ" itemp = reshape(temperature_equi, tt)


define symbol style1 = /color=red/thick=2/DASH=(0.04,0.04,0.04,0.04)
plot($linestyle)($style1)/axes=1,1,1,0($vlimits)/set tempr
  ppl ylab Temp (Deg C)
  ($taxis_setting)
  ppl title " "
  ppl xlab " "
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
ppl plot

define symbol style2 = /color=blue/thick=2
plot($linestyle)($style2)/over/nolab itemp

set view full2


! Compute the range, draw the plot centered on zero.
let drange = ABS(minmax(dtemp))
let amin = `-1.1*drange[i=@max]`
let amax = ` 1.1*drange[i=@max]`

GO preview_xy_range amin amax 1
LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)

IF `yax_lo EQ yax_lo` THEN 
   let yax_lo = `yax_lo-1`
   let yax_hi = `yax_hi+1`
ENDIF

! Not interested in tiny differences
IF `drange[i=@max] LT 0.3` THEN
   let yax_lo = -1
   let yax_hi =  1
ENDIF

DEFINE SYMBOL vlimits = /vlim=`yax_lo`:`yax_hi`

define symbol style3 = /color=black/thick=1/DASH=(0.15,0.05,0.15,0.05)
plot($linestyle)($style3)($vlimits)/axes=1,1,0,1/set dtemp
  ppl ylab Difference  (Deg C)
  if `dtemp[L=@ngd] eq 0` THEN ppl ylab "delta-temp (NO VALID DATA)"
  ($taxis_setting)
  ppl title " "
  ppl xlab " "
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
ppl plot

! Label at the top with the Expocode and count
DEFINE SYMBOL lab_top = Expocode `EXPOCODE`
LABEL/NOUSER  `($ppl$xlen)/2`,`($ppl$ylen)+0.6*($ppl$yorg)`, 0,0,0.13,  ($lab_top)

label/nouser  `($ppl$xlen)/2`,`($ppl$ylen)+0.3*($ppl$yorg)`, 0,0,0.10, Temperatures  `nobs` locations

! If no data, add a label
IF `tempr[L=@ngd] + dtemp[L=@ngd] + itemp[L=@ngd]  EQ 0` THEN \
   LABEL/nouser `($ppl$xlen)/2`,`($ppl$ylen)/2`, 0, 0, 0.15, No Valid Data

! Make the color key

GO keybox ($style1) "Temp intake" ($style2) "Temp equ" ($style3) "Difference"


FRAME/FILE="($result_plot_image_filename)"


