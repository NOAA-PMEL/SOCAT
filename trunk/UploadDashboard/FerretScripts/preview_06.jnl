! XCO2

go preview_setup ($infile_dsg) 6

! lines blk, red, green, blue/thick=2

let/title="@AS@ SST wet" sstwet = reshape(xCO2_water_sst_wet_ppm, tt)
let/title="@AS@ SST dry" sstdry = reshape(xCO2_water_sst_dry_ppm, tt)
let/title="@AS@ Teq wet" teqwet = reshape(xCO2_water_equi_temp_wet_ppm, tt)
let/title="@AS@ Teq dry" teqdry = reshape(xCO2_water_equi_temp_dry_ppm, tt)

 ! Plot variables together to set common vertical scale
plot  sstwet, sstdry, teqwet, teqdry 
CANCEL VIEW
SET VIEW full

LET amin = ($yaxis_min)
LET amax = ($yaxis_max)
GO preview_xy_range amin amax 1

LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)

DEFINE SYMBOL vlimits = /vlim=`yax_lo`:`yax_hi`


! Plot xCO2 variables 

! plot lines with individual line styles
define symbol style1 = /color=black
define symbol leglab1 "@AS@ SST wet"

LET has_data = sstwet[L=@ngd] + sstdry[L=@ngd] + teqwet[L=@ngd] + teqdry[L=@ngd]

plot($linestyle)($vlimits)/titl=" "/set sstwet
  ($taxis_setting)
  ppl ylab " "
  IF `has_data GT 0` THEN ppl ylab xCO_2 (`xCO2_water_sst_wet_ppm,return=units`)
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
ppl plot

define symbol style2 = /color=red/thick=2/DASH=(0.04,0.04,0.04,0.04)
define symbol leglab2 "@AS@ SST dry"
plot($linestyle)($style2)/over/nolab sstdry

define symbol style3 =  /color=green/thick=2/DASH=(0.15,0.05,0.15,0.05)
define symbol leglab3 "@AS@ Teq wet"
plot($linestyle)($style3)/over/nolab teqwet

define symbol style4 =  /color=blue/thick=2
define symbol leglab4 "@AS@ Teq dry"
plot($linestyle)($style4)/over/nolab teqdry

! Label at the top with the Expocode and count
DEFINE SYMBOL lab_top = Expocode `EXPOCODE`
LABEL/NOUSER  `($ppl$xlen)/2`,`($ppl$ylen)+0.6*($ppl$yorg)`, 0,0,0.13,  ($lab_top)

label/nouser  `($ppl$xlen)/2`,`($ppl$ylen)+0.3*($ppl$yorg)`, 0,0,0.10, xCO_2  `nobs` locations

! If no data, add a label
IF `has_data EQ 0` THEN \
   LABEL/nouser `($ppl$xlen)/2`,`($ppl$ylen)/2`, 0, 0, 0.15, No Valid Data

go keybox ($style1) "($leglab1)" ($style2) "($leglab2)" ($style3) "($leglab3)" ($style4) "($leglab4)"


FRAME($trans)/FORMAT=gif/FILE="($result_plot_image_filename)"
