! XCO2

go preview_setup ($infile_dsg) 6

! lines blk, red, green, blue

let/title="SST wet" sstwet = reshape(xCO2_water_sst_wet_ppm, tt)
let/title="SST dry" sstdry = reshape(xCO2_water_sst_dry_ppm, tt)
let/title="Teq wet" teqwet = reshape(xCO2_water_equi_temp_wet_ppm, tt)
let/title="Teq dry" teqdry = reshape(xCO2_water_equi_temp_dry_ppm, tt)

! Plot variables together to set common vertical scale
plot  sstwet, sstdry, teqwet, teqdry 
if `sstwet[l=@ngd] eq 0` THEN let/title="SST wet" sstwet = 1000*tt
if `sstdry[l=@ngd] eq 0` THEN let/title="SST dry" sstdry = 1000*tt
if `teqwet[l=@ngd] eq 0` THEN let/title="Teq wet" teqwet= 1000*tt
if `teqdry[l=@ngd] eq 0` THEN let/title="Teq dry" teqdry = 1000*tt
let ymin = ($yaxis_min)
let ymax = ($yaxis_max)

! First draw a top axis labeled with # obs
go preview_top_axis

! Plot xCO2 variables 
set view full

plot/line($linestyle)/axes=0,1,1,1/vlim=`ymin`:`ymax`/key=title/set  sstwet, sstdry, teqwet, teqdry 
  ($taxis_setting)
  ppl ylab xCO_2 (`xCO2_water_sst_wet_ppm,return=units`)
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
ppl plot

def sym lab_top = Expocode `EXPOCODE`
label/nouser  `($ppl$xlen)/2`,`($ppl$ylen)+0.6*($ppl$yorg)`, 0,0,0.13,  ($lab_top)


FRAME($trans)/FORMAT=gif/FILE="($result_plot_image_filename)"
