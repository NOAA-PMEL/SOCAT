! Tdiff and fco2_insitu temp

go preview_setup ($infile_dsg) 15


! Plot delta-temperature using left axis and xco2's using right axis

let/title="`delta_temp,return=title`"/units="`delta_temp,return=units`" dtemp = reshape(delta_temp, tt)
let either_teq = if xCO2_water_equi_temp_wet_ppm then xCO2_water_equi_temp_wet_ppm else xCO2_water_equi_temp_dry_ppm
let/title="@AS@ Teq" teq = reshape(either_teq, tt)
let/title="`fCO2_recommended,return=title`" fco2 = reshape(fco2_recommended, tt)

! Get vertical scale for 2 variables together
PLOT teq, fco2
CANCEL VIEW
SET VIEW full

LET amin = ($yaxis_min)
LET amax = ($yaxis_max)
GO preview_xy_range amin amax 1

LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)

DEFINE SYMBOL vlimits = /vlim=`yax_lo`:`yax_hi`

! Take care of pathological cases in the limits/bad values. pplus should handle this ok
IF `ABS(yax_hi - yax_lo) GT 1000` THEN CANCEL SYMBOL vlimits

define symbol style1 = /color=red/thick=2/DASH=(0.04,0.04,0.04,0.04)
define symbol style2 = /color=blue/thick=2
define symbol style3 = /color=black/thick=1

plot($linestyle)($style1)/axes=1,1,1,0($vlimits)/set teq
  ppl ylab "fCO_2 recommended (`fco2_recommended,return=units`)"
  if `teq[L=@ngd] eq 0 and fco2[L=@ngd] EQ 0` THEN ppl ylab fCO_2 and xCO_2 Teq (NO VALID DATA)
  ($taxis_setting)
  ppl title " "
  ppl xlab " "
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  let labdat = ($labnum_dset)
  let labttl = ($labnum_datitl)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
ppl plot

plot($linestyle)($style2)/over/nolab fco2

! delta temp, with axis on the right
set view full2

! Compute the range, draw the plot centered on zero.
let drange = ABS(minmax(dtemp))
let amin = `-1.1*drange[i=@max]`
let amax = ` 1.1*drange[i=@max]`

GO preview_xy_range amin amax 1

LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)

DEFINE SYMBOL vlimits = /vlim=`yax_lo`:`yax_hi`

! Take care of pathological cases in the limits/bad values. pplus should handle this ok
IF `ABS(yax_hi - yax_lo) GT 1000` THEN CANCEL SYMBOL vlimits

plot($linestyle)($style3)($vlimits)/axes=1,1,0,1/set dtemp
  ppl ylab "`delta_temp,return=title`  (Deg C)"
  if `dtemp[L=@ngd] eq 0` THEN ppl ylab "delta-temp (NO VALID DATA)"
  ($taxis_setting)
  ppl title " "
  ppl xlab " "
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  let labdat = ($labnum_dset)
  let labttl = ($labnum_datitl)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
ppl plot

! Label at the top with the Expocode and count
DEFINE SYMBOL lab_top = Expocode `EXPOCODE`
LABEL/NOUSER  `($ppl$xlen)/2`,`($ppl$ylen)+0.6*($ppl$yorg)`, 0,0,0.13,  ($lab_top)

LABEL/NOUSER  `($ppl$xlen)/2`,`($ppl$ylen)+0.3*($ppl$yorg)`, 0,0,0.10, \
Temperature difference and fCO_2 rec `nobs` locations


! If no data, add a label
IF `dtemp[L=@ngd] + teq[L=@ngd] + fco2[L=@ngd]  EQ 0` THEN \
   LABEL/nouser `($ppl$xlen)/2`,`($ppl$ylen)/2`, 0, 0, 0.15, (NO VALID DATA)

! Make the color key

GO keybox ($style1) "xCO_2 Teq" ($style2) "`fCO2_recommended,return=title`" ($style3) "`delta_temp,return=title`"


FRAME($trans)/FORMAT=gif/FILE="($result_plot_image_filename)"


