! Year and day-of-year

go preview_setup ($infile_dsg) 3

! Plot  Year and day-of-year, each with its own vertical axis


let/title=Year yr = TAX_YEAR (tt,tt)
let/title="Day of Year" day = reshape(day_of_year, tt)


! Year

set view full
let tt1 = tt[L=1]
let ttn = tt[L=@max]
let ax1 = `tax_year(tt1,tt1)-1`
let ax2 = `tax_year(ttn,ttn)+1`

! If there's just one year, no need to evaluate function TAX_YEAR for the whole series
IF `tax_year(tt1,tt1) EQ tax_year(ttn,ttn)` THEN let/title=Year yr = `tax_year(tt1,tt1)` + 0*tt

let dy = 1
let yint = 1
if `ax2-ax1 GT 6` then 
   let dy = 2
   let yint = 2
endif
plot($linestyle)/line/color=blue/axes=1,1,1,0/title=" "/set  yr
  ($taxis_setting)
  ppl axnsig,,0
  ppl yaxis,`ax1`,`ax2`,`dy`
  ppl axlint,,`yint`
  ppl ylab Year
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
ppl plot

! Day

set view full2

plot/axes=1,1,0,1/color=green/line($linestyle)/title=" "/set day
  ppl ylab Day
  ($taxis_setting)
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
ppl plot

! Make the color key - draw a plot with blank data, but 
! with the Ferret key made by Ferret PLOT/KEY=TITLE
! Redefine color 2 to blue (and afterwards back to red)

set view full3
ppl color,2,0,0,100
plot($linestyle)/line/key=title/color/noax/set/vlim=10000:20000/L=1:5/set yr, day
  ppl ylab " "
  ppl xlab " "
  ($taxis_setting)
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
PPL PLOT

ppl axnsig,2,2

! Label at the top with the Expocode and count
DEFINE SYMBOL lab_top = Expocode `EXPOCODE`
LABEL/NOUSER  `($ppl$xlen)/2`,`($ppl$ylen)+0.6*($ppl$yorg)`, 0,0,0.13,  ($lab_top)

label/nouser  `($ppl$xlen)/2`,`($ppl$ylen)+0.3*($ppl$yorg)`, 0,0,0.10, Count: `nobs` locations

FRAME($trans)/FORMAT=gif/FILE="($result_plot_image_filename)"


! Restore color 2 definition
ppl color,2,100,0,0
