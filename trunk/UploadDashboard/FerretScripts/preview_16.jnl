! Difference between reported and recalculated fCO_2

GO preview_setup.jnl ($infile_dsg) 13

! Show the difference between our fCO2 and the end value reported by the PI 
! (typically reported fCO2 or pCO2). So if the PI also reports fCO2 at SST, 
! plot the difference between this and our recommended fCO2. Alternative:  
! if the PI reports pCO2 at SST, plot the difference between this and our 
! recommended fCO2. These are the two options. 

list fCO2_water_sst_100humidity_uatm[i=@ngd], pCO2_water_sst_100humidity_uatm[i=@ngd]
LET/title="@ASfCO2 @ sst"/units="`fCO2_water_sst_100humidity_uatm,return=units`" reported = fCO2_water_sst_100humidity_uatm

IF `reported[i=@ngd] EQ 0` THEN \
LET/title="@ASpCO2 @ sst"/units="`pCO2_water_sst_100humidity_uatm,return=units`" reported = pCO2_water_sst_100humidity_uatm


LET diff_co2 = reshape( diff, tt)
LET fco2_t = reshape(fco2_recommended, tt)
LET reported_t = reshape (reported, tt)
LET diff_t = fco2_t - reported_t

! Get vertical scale for 2 variables together
PLOT fco2_t, reported_t
CANCEL VIEW
SET VIEW full

LET amin = ($yaxis_min)
LET amax = ($yaxis_max)

GO preview_xy_range amin amax 1

LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)

DEFINE SYMBOL vlimits = /vlim=`yax_lo`:`yax_hi`

define symbol style1 = /color=red/thick=2/DASH=(0.04,0.04,0.04,0.04)
define symbol style2 = /color=blue/thick=2
define symbol style3 = /color=black/thick=1


plot($linestyle)($style1)/axes=1,1,1,0($vlimits)/set reported_t
  ppl ylab "fCO_2 (`fco2_recommended,return=units`)"
  if `reported_t[L=@ngd] eq 0 and fco2_t[L=@ngd] EQ 0` THEN ppl ylab fCO_2 and xCO_2 Teq (NO VALID DATA)
  ($taxis_setting)
  ppl title " "
  ppl xlab " "
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  let labdat = ($labnum_dset)
  let labttl = ($labnum_datitl)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
ppl plot

plot($linestyle)($style2)/over/nolab fco2_t

! delta, with axis on the right
set view full2

! Compute the range, draw the plot centered on zero.
let drange = ABS(minmax(diff_t))
let amin = `-1.1*drange[i=@max]`
let amax = ` 1.1*drange[i=@max]`
GO preview_xy_range amin amax 1

LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)

 ! Not interested in tiny differences
IF `drange[i=@max] - drange[i=@min] LT 0.5` THEN   
   LET yax_lo = -1
   LET yax_hi = 1
ENDIF

DEFINE SYMBOL vlimits = /vlim=`yax_lo`:`yax_hi`

! Take care of pathological cases in the limits/bad values. pplus should handle this ok
IF `ABS(yax_hi - yax_lo) GT 1000` THEN CANCEL SYMBOL vlimits

PLOT($linestyle)($vlimits)($style3)/title=" "/axes=0,0,0,1/SET diff_t
  ppl ylab "Difference (`fco2_recommended,return=units`)"
  ppl xlab " "
  ($taxis_setting)
  IF ($labnum_year"0|*>1") THEN GO unlabel ($labnum_year)
  IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
  IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
PPL PLOT

! Label at the top with the Expocode and count
DEFINE SYMBOL lab_top = Expocode `EXPOCODE`
LABEL/NOUSER  `($ppl$xlen)/2`,`($ppl$ylen)+0.6*($ppl$yorg)`, 0,0,0.13,  ($lab_top)
label/nouser  `($ppl$xlen)/2`,`($ppl$ylen)+0.3*($ppl$yorg)`, 0,0,0.10, Reported and recommended fCO2  `nfnoc` Valid fCO_2 recomputed



! Make the color key

GO keybox ($style2) "`fCO2_recommended,return=title`" ($style1) "Reported `reported,return=title`" ($style3) "Difference"


FRAME($trans)/FORMAT=gif/FILE="($result_plot_image_filename)"

